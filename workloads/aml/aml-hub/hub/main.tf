########## Create resource group and Log Analytics Workspace
##########
##########

## Create resource group the resources in this deployment will be deployed to
##
resource "azurerm_resource_group" "rg_aml_hub" {
  name     = "rgamlhub${var.region_code}${var.random_string}"
  location = var.region
  tags     = var.tags

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create a Log Analytics Workspace that all resources specific to this workload will
## write configured resource logs and metrics to
resource "azurerm_log_analytics_workspace" "log_analytics_workspace_workload" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub
  ]

  name                = "lawamlhub${var.region_code}${var.random_string}"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name

  sku               = "PerGB2018"
  retention_in_days = 30

  tags = var.tags

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Pause for 30 seconds to allow the Log Analytics Workspace to be fully provisioned
## and mitigate risks of Application Insights creation failure due to workspace not being
## fully available
resource "time_sleep" "wait_log_analytics_workspace" {
  depends_on = [
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  create_duration = "30s"
}

########## Create dependent resources required by Azure Machine Learning Hub
##########
##########

## Create Application Insights instance. This will be shared by the AML Hub and its projects to capture 
## logs and metrics generated by the compute resources
resource "azurerm_application_insights" "appins_aml_hub" {
  depends_on = [
    time_sleep.wait_log_analytics_workspace
  ]

  name                = "appinsamlhub${var.region_code}${var.random_string}"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  workspace_id        = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id
  application_type    = "other"
  tags                = var.tags

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create Azure Storage account which will be used as the default storage account for the AML hub and projects.
##
resource "azurerm_storage_account" "storage_account_aml_hub" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  name                = "stamlhub${var.region_code}${var.random_string}"
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  location            = var.region
  tags                = var.tags

  account_kind             = "StorageV2"
  account_tier             = "Standard"
  account_replication_type = "LRS"

  # Disable key-based access
  shared_access_key_enabled = false

  # Disable public access for blob containers
  allow_nested_items_to_be_public = false

  network_rules {
    # Block all public access by default
    default_action = "Deny"

    # Create resource access rule to allow workspaces within the subscription network access through the storage service firewall
    private_link_access {
      endpoint_resource_id = "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${azurerm_resource_group.rg_aml_hub.name}/providers/Microsoft.MachineLearningServices/workspaces/*"
    }
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create diagnostic settings for the AML Hub storage account for blob, file, queue, and table services to 
## send logs to the workload Log Analytics Workspace
resource "azurerm_monitor_diagnostic_setting" "diag_storage_aml_hub_blob" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub
  ]

  name                       = "diag-base"
  target_resource_id         = "${azurerm_storage_account.storage_account_aml_hub.id}/blobServices/default"
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "StorageRead"
  }

  enabled_log {
    category = "StorageWrite"
  }

  enabled_log {
    category = "StorageDelete"
  }
}

resource "azurerm_monitor_diagnostic_setting" "diag_storage_aml_hub_file" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub,
    azurerm_monitor_diagnostic_setting.diag_storage_aml_hub_blob
  ]

  name                       = "diag-base"
  target_resource_id         = "${azurerm_storage_account.storage_account_aml_hub.id}/fileServices/default"
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "StorageRead"
  }

  enabled_log {
    category = "StorageWrite"
  }

  enabled_log {
    category = "StorageDelete"
  }
}

resource "azurerm_monitor_diagnostic_setting" "diag_storage_aml_hub_queue" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub,
    azurerm_monitor_diagnostic_setting.diag_storage_aml_hub_file
  ]

  name                       = "diag-base"
  target_resource_id         = "${azurerm_storage_account.storage_account_aml_hub.id}/queueServices/default"
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "StorageRead"
  }

  enabled_log {
    category = "StorageWrite"
  }

  enabled_log {
    category = "StorageDelete"
  }
}

resource "azurerm_monitor_diagnostic_setting" "diag_storage_aml_hub_table" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub,
    azurerm_monitor_diagnostic_setting.diag_storage_aml_hub_queue
  ]

  name                       = "diag-base"
  target_resource_id         = "${azurerm_storage_account.storage_account_aml_hub.id}/tableServices/default"
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "StorageRead"
  }

  enabled_log {
    category = "StorageWrite"
  }

  enabled_log {
    category = "StorageDelete"
  }
}

## Create Key Vault that will hold secrets for AML Hub and project-level connections that use API keys
##
resource "azurerm_key_vault" "key_vault_aml_hub" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  name                = "kvamlhub${var.region_code}${var.random_string}"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tenant_id           = data.azurerm_client_config.current.tenant_id
  tags                = var.tags


  sku_name = "standard"

  # Enabled for RBAC authorization
  rbac_authorization_enabled = true

  # Turn off purge protection so that Vault can be immediately purged
  purge_protection_enabled   = false
  soft_delete_retention_days = 7

  # Not required for this implementation
  enabled_for_disk_encryption     = false
  enabled_for_deployment          = false
  enabled_for_template_deployment = false

  network_acls {
    default_action             = "Deny"
    bypass                     = "AzureServices"
    virtual_network_subnet_ids = []
    ip_rules = [
      var.trusted_ip
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create diagnostic settings for the AML Hub Key Vault to send logs to the workload Log Analytics Workspace
##
resource "azurerm_monitor_diagnostic_setting" "diag_key_vault_aml_hub" {
  depends_on = [
    azurerm_key_vault.key_vault_aml_hub
  ]

  name                       = "diag"
  target_resource_id         = azurerm_key_vault.key_vault_aml_hub.id
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "AuditEvent"
  }

  enabled_log {
    category = "AzurePolicyEvaluationDetails"
  }
}

## Create an Azure Container Registry instance for use by the AML Hub and projects
##
resource "azurerm_container_registry" "acr_aml_hub" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  name                = "acramlhub${var.region_code}${var.random_string}"
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  location            = var.region
  tags                = var.tags

  # Premium SKU required for Private Endpoints
  sku = "Premium"

  # Disable the local admin user of the Container Registry
  admin_enabled = false

  # Disable anonymous pull access
  anonymous_pull_enabled = false

  identity {
    type = "SystemAssigned"
  }

  public_network_access_enabled = false
  network_rule_set {
    default_action = "Deny"
  }
  network_rule_bypass_option = "AzureServices"

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create diagnostic settings for AML Hub Container Registry to send logs to workload Log Analytics Workspace
##
resource "azurerm_monitor_diagnostic_setting" "diag_acr_aml_hub" {
  depends_on = [
    azurerm_container_registry.acr_aml_hub
  ]

  name                       = "diag-base"
  target_resource_id         = azurerm_container_registry.acr_aml_hub.id
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "ContainerRegistryRepositoryEvents"
  }
  enabled_log {
    category = "ContainerRegistryLoginEvents"
  }
}

########## Create the resources required when the AML Hub to support CMK encryption of the Hub
##########
##########

## Create the Azure Key Vault instance which will be used to store the key to support CMK encryption of the AML Hub
##
resource "azurerm_key_vault" "key_vault_aml_hub_cmk" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  name                = "kvcmkamlhub${var.region_code}${var.random_string}"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tenant_id           = data.azurerm_client_config.current.tenant_id
  tags                = var.tags

  sku_name = "standard"

  # Disable RBAC authorization because AML Hubs don't yet support access with UMIs and require access policies
  rbac_authorization_enabled = false

  # Turn on purge protection because it is required for CMK usage
  purge_protection_enabled   = true
  soft_delete_retention_days = 7

  # Not required for this implementation
  enabled_for_disk_encryption     = false
  enabled_for_deployment          = false
  enabled_for_template_deployment = false

  network_acls {
    default_action             = "Deny"
    bypass                     = "AzureServices"
    virtual_network_subnet_ids = []
    ip_rules = [
      var.trusted_ip
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create diagnostic settings for Key Vault used to store the CMK for the AML Hub and projects to send logs to the workload Log Analytics Workspace
##
resource "azurerm_monitor_diagnostic_setting" "diag_key_vault_aml_hub_cmk" {
  depends_on = [
    azurerm_key_vault.key_vault_aml_hub_cmk
  ]

  name                       = "diag"
  target_resource_id         = azurerm_key_vault.key_vault_aml_hub_cmk.id
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "AuditEvent"
  }

  enabled_log {
    category = "AzurePolicyEvaluationDetails"
  }
}

## Required only for my lab environment
## Create access policy to allow Terraform service principal to create and manage keys in the Key Vault
##
resource "azurerm_key_vault_access_policy" "access_policy_terraform_aml_hub_key_permissions" {
  depends_on = [
    azurerm_key_vault.key_vault_aml_hub_cmk
  ]

  key_vault_id = azurerm_key_vault.key_vault_aml_hub_cmk.id
  tenant_id    = data.azurerm_client_config.current.tenant_id
  object_id    = data.azurerm_client_config.current.object_id

  key_permissions = [
    "Get",
    "Create",
    "Delete",
    "List",
    "Update",
    "Import",
    "Recover",
    "Backup",
    "Restore",
    "GetRotationPolicy",
    "SetRotationPolicy",
    "Rotate"
  ]
}

## Create the CMK that will be used to encrypt the AML Hub and projects
##
resource "azurerm_key_vault_key" "key_aml_hub_cmk" {
  depends_on = [
    azurerm_key_vault.key_vault_aml_hub_cmk,
    azurerm_key_vault_access_policy.access_policy_terraform_aml_hub_key_permissions
  ]

  name         = "cmkamlhub"
  key_vault_id = azurerm_key_vault.key_vault_aml_hub_cmk.id
  key_type     = "RSA"

  # Don't use less than 4096 or else it will be upped to 4096 causing recreation of the key on every re-apply
  key_size = 4096
  key_opts = ["decrypt", "encrypt", "sign", "unwrapKey", "verify", "wrapKey"]

  # Prevent Terraform from deleting and recreating the key on each apply
  lifecycle {
    ignore_changes = [
      expiration_date,
      not_before_date,
      tags,
      rotation_policy
    ]
  }
}

########## Create the AML Hub and its diagnostic settings
##########
##########

## Create the AML Hub Workspace
##
resource "azapi_resource" "aml_hub" {
  depends_on = [
    azurerm_resource_group.rg_aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload,
    azurerm_application_insights.appins_aml_hub,
    azurerm_container_registry.acr_aml_hub,
    azurerm_storage_account.storage_account_aml_hub,
    azurerm_key_vault.key_vault_aml_hub,
    azurerm_key_vault.key_vault_aml_hub_cmk,
    azurerm_key_vault_key.key_aml_hub_cmk
  ]

  type                      = "Microsoft.MachineLearningServices/workspaces@2025-09-01"
  name                      = "amlhub${var.region_code}${var.random_string}"
  parent_id                 = azurerm_resource_group.rg_aml_hub.id
  location                  = var.region
  schema_validation_enabled = true

  body = {
    # Set the hub to use a user-assigned managed identity if specified, otherwise use system-assigned
    identity = {
      type = "SystemAssigned"
    }

    tags = var.tags

    # Create an AML hub workspace
    kind = "Hub"

    properties = {
      friendlyName = "Sample-AML-Hub"
      description  = "This is a sample AML Hub"

      applicationInsights = azurerm_application_insights.appins_aml_hub.id
      keyVault            = azurerm_key_vault.key_vault_aml_hub.id
      storageAccount      = azurerm_storage_account.storage_account_aml_hub.id
      containerRegistry   = azurerm_container_registry.acr_aml_hub.id

      # Enable the HBI feature for additional security since there are few reasons not to
      hbiWorkspace = true

      # Enable Service-Side CMK encryption
      enableServiceSideCMKEncryption = true

      # Enable CMK encryption if specified in the hub_encryption variable. As of 10/2025 AML Hub must use SMI
      encryption = {
        status = "Enabled"
        keyVaultProperties = {
          keyVaultArmId = azurerm_key_vault.key_vault_aml_hub_cmk.id
          keyIdentifier = azurerm_key_vault_key.key_aml_hub_cmk.id
        }
      }

      # Block access to the AML Workspace over the public endpoint
      publicNetworkAccess = "Disabled"
      managedNetwork = {
        # Managed virtual network will block all outbound traffic unless explicitly allowed
        isolationMode = "AllowOnlyApprovedOutbound"
        # Use Azure Firewall Standard SKU to support FQDN-based rules
        firewallSku = "Standard"
        outboundRules = merge(
          {
          },
          local.vscode_ssh_outbound_fqdn_rules,
          local.python_library_outbound_fqdn_rules,
          local.conda_library_outbound_fqdn_rules,
          local.docker_outbound_fqdn_rules,
          local.huggingface_outbound_fqdn_rules,
          local.user_defined_outbound_pe_rules,
          # This ruleset is only required for the lab
          local.user_defined_outbound_fqdn_rules
        )
      }

      # Set the authentication for system datastores to use the managed identity of the hub instead of storing the API keys as secrets in Key Vault
      systemDatastoresAuthMode = "identity"

      # Place the workspace in the resource group created at the beginning of this template
      workspaceHubConfig = {
        defaultWorkspaceResourceGroup = azurerm_resource_group.rg_aml_hub.id
      }
    }
  }

  response_export_values = [
    "identity.principalId",
    "properties.workspaceId"
  ]

  # Ignore updates to these tags on additional Terraform deployments
  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create diagnostic settings to capture resource logs from the AML Hub and send them to the workload Log Analytics Workspace
##
resource "azurerm_monitor_diagnostic_setting" "diag_aml_hub" {
  depends_on = [
    azapi_resource.aml_hub,
    azurerm_log_analytics_workspace.log_analytics_workspace_workload
  ]

  name                       = "diag-base"
  target_resource_id         = azapi_resource.aml_hub.id
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_analytics_workspace_workload.id

  enabled_log {
    category = "ComputeInstanceEvent"
  }
}

########## Create additional role assignments that for AML Hub system-assigned managed identity that require the AML Hub to exist first
########## These are only required if the hub_identity variable is set to 'smi'.
##########

## Sleep for 120 seconds to allow the AML system-assigned managed identity to replicate through Entra ID and
## required role assignments to be created by the platform and replicated through Azure
resource "time_sleep" "wait_aml_hub_system_identity" {
  depends_on = [
    azapi_resource.aml_hub
  ]

  create_duration = "120s"
}

## Create Azure RBAC role assignment on the AML Hub resource group for the AML Hub system-assigned managed identity
## to assign the Azure AI Enterprise Network Connection Approver role. This is required for the workspace to create
## the managed private endpoints in the managed virtual network
resource "azurerm_role_assignment" "smi_aml_rg_azure_ai_net_conn_app" {
  depends_on = [
    time_sleep.wait_aml_hub_system_identity
  ]

  name                 = uuidv5("dns", "${azurerm_resource_group.rg_aml_hub.name}${azapi_resource.aml_hub.output.identity.principalId}netconnapp")
  scope                = azurerm_resource_group.rg_aml_hub.id
  role_definition_name = "Azure AI Enterprise Network Connection Approver"
  principal_id         = azapi_resource.aml_hub.output.identity.principalId
}

## Sleep for 120 seconds to allow this new permission to replicate through Azure
##
resource "time_sleep" "wait_aml_hub_role_assignments_smi" {
  depends_on = [
    azurerm_role_assignment.smi_aml_rg_azure_ai_net_conn_app
  ]
  create_duration = "120s"
}

########## Create Private Endpoints for AML Hub and required resources
##########
##########

## Create a Private Endpoint AML Hub instance
##
resource "azurerm_private_endpoint" "pe_aml_hub" {
  depends_on = [
    azapi_resource.aml_hub
  ]

  name                = "pe${azapi_resource.aml_hub.name}workspace"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tags                = var.tags
  subnet_id           = var.subnet_id_private_endpoints

  custom_network_interface_name = "nic${azapi_resource.aml_hub.name}workspace"

  private_service_connection {
    name                           = "peconn${azapi_resource.aml_hub.name}workspace"
    private_connection_resource_id = azapi_resource.aml_hub.id
    subresource_names              = ["amlworkspace"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name = "zoneconn${azapi_resource.aml_hub.name}workspace"
    private_dns_zone_ids = [
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.api.azureml.ms",
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.notebooks.azure.net"
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create a Private Endpoint for AML Hub storage account blob endpoint
##
resource "azurerm_private_endpoint" "pe_storage_account_aml_hub_blob" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub
  ]

  name                = "pe${azurerm_storage_account.storage_account_aml_hub.name}blob"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tags                = var.tags
  subnet_id           = var.subnet_id_private_endpoints

  custom_network_interface_name = "nic${azurerm_storage_account.storage_account_aml_hub.name}blob"

  private_service_connection {
    name                           = "peconn${azurerm_storage_account.storage_account_aml_hub.name}blob"
    private_connection_resource_id = azurerm_storage_account.storage_account_aml_hub.id
    subresource_names              = ["blob"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name = "zoneconn${azurerm_storage_account.storage_account_aml_hub.name}blob"
    private_dns_zone_ids = [
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.blob.core.windows.net"
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create a Private Endpoint for AML Hub storage account file endpoint
##
resource "azurerm_private_endpoint" "pe_storage_account_aml_hub_file" {
  depends_on = [
    azurerm_storage_account.storage_account_aml_hub,
    azurerm_private_endpoint.pe_storage_account_aml_hub_blob
  ]

  name                = "pe${azurerm_storage_account.storage_account_aml_hub.name}file"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tags                = var.tags
  subnet_id           = var.subnet_id_private_endpoints

  custom_network_interface_name = "nic${azurerm_storage_account.storage_account_aml_hub.name}file"

  private_service_connection {
    name                           = "peconn${azurerm_storage_account.storage_account_aml_hub.name}file"
    private_connection_resource_id = azurerm_storage_account.storage_account_aml_hub.id
    subresource_names              = ["file"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name = "zoneconn${azurerm_storage_account.storage_account_aml_hub.name}file"
    private_dns_zone_ids = [
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.file.core.windows.net"
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create a Private Endpoint for AML Hub Key Vault used to store secret for AML connections
##
resource "azurerm_private_endpoint" "pe_key_vault_aml_hub" {
  depends_on = [
    azurerm_key_vault.key_vault_aml_hub,
    azurerm_private_endpoint.pe_storage_account_aml_hub_file
  ]

  name                = "pe${azurerm_key_vault.key_vault_aml_hub.name}vault"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tags                = var.tags
  subnet_id           = var.subnet_id_private_endpoints

  custom_network_interface_name = "nic${azurerm_key_vault.key_vault_aml_hub.name}vault"

  private_service_connection {
    name                           = "peconn${azurerm_key_vault.key_vault_aml_hub.name}vault"
    private_connection_resource_id = azurerm_key_vault.key_vault_aml_hub.id
    subresource_names              = ["vault"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name = "zoneconn${azurerm_key_vault.key_vault_aml_hub.name}vault"
    private_dns_zone_ids = [
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.vaultcore.azure.net"
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

## Create a Private Endpoint for AML Hub Container Registry
##
resource "azurerm_private_endpoint" "pe_container_registry_aml_hub" {
  depends_on = [
    azurerm_container_registry.acr_aml_hub,
    azurerm_private_endpoint.pe_key_vault_aml_hub
  ]

  name                = "pe${azurerm_container_registry.acr_aml_hub.name}registry"
  location            = var.region
  resource_group_name = azurerm_resource_group.rg_aml_hub.name
  tags                = var.tags
  subnet_id           = var.subnet_id_private_endpoints

  custom_network_interface_name = "nic${azurerm_container_registry.acr_aml_hub.name}registry"

  private_service_connection {
    name                           = "peconn${azurerm_container_registry.acr_aml_hub.name}registry"
    private_connection_resource_id = azurerm_container_registry.acr_aml_hub.id
    subresource_names              = ["registry"]
    is_manual_connection           = false
  }

  private_dns_zone_group {
    name = "zoneconn${azurerm_container_registry.acr_aml_hub.name}registry"
    private_dns_zone_ids = [
      "/subscriptions/${data.azurerm_subscription.current.subscription_id}/resourceGroups/${var.resource_group_name_dns}/providers/Microsoft.Network/privateDnsZones/privatelink.azurecr.io"
    ]
  }

  lifecycle {
    ignore_changes = [
      tags["created_date"],
      tags["created_by"]
    ]
  }
}

########## Create DNS record required for split-brain DNS of instances.azureml.ms which is required zone
########## to support access to compute instances from Visual Studio Code and AML Studio when using notebooks
##########

## Create auth A record in the instances.azureml.ms private DNS zone for the AML Hub workspace
##
resource "azurerm_private_dns_a_record" "aml_compute_instance_dns_record_auth" {
  depends_on = [
    azurerm_private_endpoint.pe_aml_hub
  ]

  name                = "auth.${var.region}"
  zone_name           = "instances.azureml.ms"
  resource_group_name = var.resource_group_name_dns
  ttl                 = 10
  records = [
    azurerm_private_endpoint.pe_aml_hub.private_service_connection.0.private_ip_address
  ]
}

